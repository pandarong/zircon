# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("$zx/public/gn/config/standard.gni")
import("environment.gni")
import("toolchain.gni")

assert(current_toolchain == default_toolchain,
       "Define toolchains only in `default_toolchain` $default_toolchain")

# The stub toolchain is the $default_toolchain set by //gn/BUILDCONFIG.gn.
# It has nothing but the standard stamp and copy tools, so it can only be
# used for action and copy rules.
define_toolchain("stub") {
  tools = []
}

# Define toolchains for building host tools.  Always define a toolchain for
# each supported host, not just the current one.  It's always possible to
# cross-compile the host tools for a different host.
standard_host = false
foreach(host, standard_build_hosts) {
  define_environment("host") {
    cpu = host.cpu
    os = host.os
    globals = {
      is_host = true
    }
    strip = true
  }
  if (host.cpu == host_cpu && host.os == host_os) {
    standard_host = true
  }
}

assert(standard_host,
       "$host_cpu-$host_os is not a standard build host for Fuchsia")

# Define toolchains for building userland.
foreach(cpu, standard_fuchsia_cpus) {
  define_environment("user") {
    cpu = cpu
    configs += standard_fuchsia_configs + standard_fuchsia_user_configs
    shlib = true
    strip = "--strip-sections"
  }
}
