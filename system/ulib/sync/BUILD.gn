# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

library("sync") {
  sdk = "static"
  sources = [
    "completion.c",
    "condition.cpp",
    "mutex.c",
  ]
  configs += [ "$zx/public/gn/config:visibility_hidden" ]
  deps = [
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zircon-internal",
  ]

  if (toolchain.environment == "user") {
    # This library is used by libc, so suppress the circularity.
    configs -= [ "$zx/public/gn/config:user" ]
    configs += [
      # This code is used in early startup, where safe-stack setup is not
      # ready yet.
      "$zx/public/gn/config:no_sanitizers",
      "$zx/system/ulib/c:headers",
    ]
  }
}
